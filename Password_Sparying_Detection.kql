let SigninLogs = datatable(User:string, IP:string, Result:string, Location:string, Time:datetime)
[
    "asad",   "10.1.1.1",      "Success", "Ireland", datetime(2025-01-01T10:00:00),
    "john",   "22.45.91.2",    "Failed",  "China",   datetime(2025-01-01T10:10:00),
    "ankit",  "99.21.4.9",     "Success", "India",   datetime(2025-01-01T10:20:00),
    "asad",   "45.12.90.10",   "Failed",  "Unknown", datetime(2025-01-01T11:00:00),
    "john",   "45.12.90.10",   "Failed",  "Unknown", datetime(2025-01-01T11:01:00),
    "ankit",  "45.12.90.10",   "Failed",  "Unknown", datetime(2025-01-01T11:02:00),
    "katie",  "45.12.90.10",   "Failed",  "Unknown", datetime(2025-01-01T11:03:00),
    "alice",  "45.12.90.10",   "Failed",  "Unknown", datetime(2025-01-01T11:04:00),
    "john",   "45.12.90.10",   "Success", "Unknown", datetime(2025-01-01T11:05:00),
    "asad",   "100.34.22.9",   "Failed",  "Russia",  datetime(2025-01-01T12:00:00),
    "john",   "100.34.22.9",   "Failed",  "Russia",  datetime(2025-01-01T12:01:00),
    "katie",  "100.34.22.9",   "Failed",  "Russia",  datetime(2025-01-01T12:02:00),
    "alice",  "100.34.22.9",   "Failed",  "Russia",  datetime(2025-01-01T12:03:00),
    "ankit",  "100.34.22.9",   "Success", "Russia",  datetime(2025-01-01T12:05:00),
    "asad",   "77.19.88.20",   "Failed",  "Brazil",  datetime(2025-01-01T14:00:00),
    "john",   "77.19.88.20",   "Failed",  "Brazil",  datetime(2025-01-01T14:01:00),
    "ankit",  "77.19.88.20",   "Failed",  "Brazil",  datetime(2025-01-01T14:02:00),
    "katie",  "77.19.88.20",   "Failed",  "Brazil",  datetime(2025-01-01T14:03:00),
    "alice",  "77.19.88.20",   "Failed",  "Brazil",  datetime(2025-01-01T14:04:00),
    "alice",  "71.22.14.3",    "Failed",  "USA",     datetime(2025-01-01T16:30:00),
    "katie",  "88.19.23.5",    "Failed",  "USA",     datetime(2025-01-01T16:31:00)
];
SigninLogs
| where Result == "Failed" // Filtering out only the Failed Events
| summarize TargetedUser= dcount(User), // Summaririzing Unique users 
FailedAttempts= count(), // Counting all failed attempts 
AttackedUser= make_set(User),// Array of attacked user
LastFailed= max(Time)// Last time for Failed Attempt
by IP
| where TargetedUser >= 3 // Suspicious IPs targeting 3+ users 
| join kind = inner (SigninLogs) on IP // Joining with the original table 
| where Result == "Success" // Checking if the attacker succesfully logged in 
|extend TimeDiff = datetime_diff("minute", Time, LastFailed)// Findind the time diif between succesful and failed log in 
| where TimeDiff between (0 ..5 )// Checking if the log in was done between 0 to 5 mins
| project User,IP,FailedAttempts,AttackedUser,TimeDiff,Result,TargetedUser,Location,Time// Projecting the necessary fields 
| as CompromisedEvents;// Naming the table as CompromisedEvents
CompromisedEvents
| summarize 
    TotalFailedAttempts = sum(FailedAttempts),// Volume of Spray from attacker
    UniqueUsersTargeted = sum(TargetedUser),// Total unique users
    CompromisedUsers = make_set(User),// Which user's account got compromised
    AllAttackedUsers = make_set(AttackedUser),// Every User which got atacked
    AverageTimeToCompromise = avg(TimeDiff)// Average time b/w success and final fail
    by IP // Creating a Summary of Events 
| as EventSummary